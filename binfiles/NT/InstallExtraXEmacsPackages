#!/usr/bin/env zsh

# Download and install XEmacs packages that are not
# installed by default in the XEmacs Windows setup kit.

emulate -LR zsh
progname=${0:t}

: ${XEMACS_KIT_BUILD_DIR:=/XEmacsBuild}
: ${XEMACS_KIT_PKG_DIR:=$XEMACS_KIT_BUILD_DIR/packages}
: ${XEMACS_INSTALL_ROOT:=/XEmacs}
: ${SITE_PKG_INSTALL_DIR:=$XEMACS_INSTALL_ROOT/site-packages}
: ${XEMACS_PKG_INSTALL_DIR:=$XEMACS_INSTALL_ROOT/xemacs-packages}
: ${XEMACS_PKGS:="
    c-support    calc         cc-mode      debug        dired
    easypg       ecb          ecrypto      edebug       ediff
    edit-utils   efs          eieio        fsf-compat   gnus
    ibuffer      igrep        ispell       mail-lib     net-utils
    os-utils     pc           pcl-cvs      perl-modes   prog-modes
    ps-print     python-modes ruby-modes   semantic     sh-script
    sounds-wav   speedbar     texinfo      text-modes   time
    vc           xemacs-base  xemacs-devel"}
: ${ZIPROOT:=~/software/zip}
: ${SITE_PKGS:="auctex riece"}

# Install packages included in kit, but not installed in the minimal install.
# This allows me not to think and to install the smallest kit.
for p in ${=XEMACS_PKGS}; do
  print "Installing $p in $XEMACS_PKG_INSTALL_DIR"
  tar xf $XEMACS_KIT_PKG_DIR/$p-*-pkg.tar(om[1]) -C $XEMACS_PKG_INSTALL_DIR
done

# Included here mostly for documentation purposes.
if [[ -n $download ]]; then
: ${auctex_ver:=11.85}
: ${riece_ver:=1.23}
  wget http://ftp.gnu.org/pub/gnu/auctex/auctex-${auctex_ver}-pkg.tar.gz{,.sig}
  gpg --verify auctex-${auctex_ver}-pkg.tar.gz.sig

  wget http://ftp.xemacs.org/pub/xemacs/packages/riece-${riece_ver}-pkg.tar.gz
fi

for p in ${=SITE_PKGS}; do
  print "Installing $p in $SITE_PKG_INSTALL_DIR"
  tar xfz $ZIPROOT/$p*-pkg.tar.gz(om[1]) -C $SITE_PKG_INSTALL_DIR
done

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
