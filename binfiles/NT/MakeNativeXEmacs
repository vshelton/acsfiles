#!/usr/bin/env zsh

# Build a Windows-native version of XEmacs.
# Usage: MakeNativeXEmacs [-h|--help] [--with-mule] [--without-mule] [--with-debug] [--without-debug] [--package-root=path] [--prefix=path] [--install] <21.4|21.5>
#
# Assumptions:
#   1) There is an SCM hierarchy rooted at $SCM_ROOT (defaults to ~/cvsroot
#      for whatever XEmacs version is specified.
#   2) Cygwin is installed on the build machine, including makeinfo, perl,
#      cvs, tar and probably other stuff, too.
#   3) Optional library sources are in /usr/local/src. (See LIB_DIR.)
#   4) zlib is in /usr/local/src/zlib (See ZLIB_VER.)
#   5) tiff is installed in /usr/local/src/tiff-3.7.3 (See TIFF_VER.)
#   6) Visual Studio is installed in E:/Program Files/Microsoft Visual Studio
#      (See VC_ROOT.)

# Set up a vanilla zsh environment
emulate -LR zsh
progname=${0:t}
0=$progname                             # For error messages from builtins

# Update to the latest version of the sources.
# This started with just CVS, but now supports multiple SCM systesm.
function update_sources {
  scm_update xemacs-$XEMACS_VER
}

function usage {
  print -u2 "\
usage: $progname [-h|--help] [--with-mule] [--without-mule] [--with-debug] [--without-debug] [--package-root dir] [--prefix dir] [--install] [--intel] [--sdk] <21.4|21.5|21.5-test>
       -h|--help            print this help message and exit
       --with-mule          turn on MULE support
       --without-mule       turn off MULE support
       --with-debug         turn on debugging
       --without-debug      turn off debugging
       --package-root=dir   specify directory for root of installed packages
       --prefix=dir         specify installation directory
       --install            install the executable
       --intel              use the intel compiler
       --sdk                build with the Microsoft SDK
       21.4                 build a 21.4 XEmacs kit from SCM
       21.5                 build a 21.5 XEmacs kit from SCM
       21.5-test            build a 21.5 XEmacs kit from the latest SCM test sources"
}

# Things to customize
: ${SCM_ROOT:=~/cvsroot}
: ${LIB_DIR:=/usr/local/src}
: ${ZLIB_VER:=zlib}
: ${TIFF_VER:=tiff-3.7.3}

# Visual Studio and Platform SDK root directories
: ${VC_ROOT:="E:/Program Files/Microsoft Visual Studio"}
: ${SDK_ROOT:="E:/Program Files/Microsoft Platform SDK"}
: ${INTEL_ROOT:="C:/Program Files/Intel/Compiler/C++/10.0.025/IA32"}

zparseopts -D h=help -help=help -with-mule=with_mule -without-mule=without_mule -with-debug=with_debug -without-debug=without_debug -package-root:=package_root -prefix:=prefix -install=install -sdk=with_sdk -intel=with_intel

if [[ -n $help ]]; then
  usage
  exit 0
fi

# Set up for the Visual Studio compiler
[[ -n $with_sdk ]] && with_sdk=SDK
if [[ -n $with_intel ]]; then
  export CC=icl
  with_intel=intel
fi
. vcsetup $with_sdk $with_intel || exit 1

# Strip out option strings and '='
install_dir=${prefix[2]#=}
package_dir=${package_root[2]#=}

# 3 options for debug and mule: with, without or no change from default
debug=
[[ -n $with_debug ]] && debug=1
[[ -n $without_debug ]] && debug=0
mule=
[[ -n $with_mule ]] && mule=1
[[ -n $without_mule ]] && mule=0

XEMACS_VER=
case $1 in
(21.4|21.5|21.5-test)
  XEMACS_VER=$1
  shift
  ;;
(*)
  print -u2 "$progname: Only XEmacs 21.4 and XEmacs 21.5 are currently supported."
  usage
  exit 1
  ;;
esac

# Update to latest sources
update_sources

# Make a local copy of the source tree
: ${VERSION:=$(today)}
SRCROOT=$PWD mk-src xemacs-$XEMACS_VER

# Configure the sources
cd xemacs-$XEMACS_VER-$VERSION/nt
unset HOME              # Don't pick up my emacs customizations
# Configure for optional packages
perl -pi -e "s@^OPTIONAL_LIBRARY_DIR=.*@OPTIONAL_LIBRARY_DIR=$(cygpath --mixed /)$LIB_DIR@;
             s@^ZLIB_DIR=.*@ZLIB_DIR=\\\$(OPTIONAL_LIBRARY_DIR)/$ZLIB_VER@;
             s@^TIFF_DIR=.*@TIFF_DIR=\\\$(OPTIONAL_LIBRARY_DIR)/$TIFF_VER@;
             s@^HAVE_XFACE=.*@HAVE_XFACE=0@;
             s@^MAKEINFO=.*@MAKEINFO=${(q)$(cygpath --windows =makeinfo.exe)}@" < config.inc.samp >config.inc

# Change installation directory if requested
[[ -n $install_dir ]] && perl -pi -e "s@^INSTALL_DIR=.*@INSTALL_DIR=$install_dir/XEmacs-\\\$(XEMACS_VERSION_STRING)@" config.inc

# Configure debugging if requested
[[ -n $debug ]] && perl -pi -e "s@^DEBUG_XEMACS=.*@DEBUG_XEMACS=$debug@" config.inc

# Don't require the C runtime debug library
# This keeps the setup kits from requiring msvcrtd.dll
perl -pi -e 's@.*BUILD_FOR_SETUP_KIT.*@BUILD_FOR_SETUP_KIT=1@' config.inc

# Configure MULE if requested (only for 21.5)
[[ -n $mule ]] &&  perl -pi -e "s@^MULE=.*@MULE=$mule@" config.inc

# Use the Intel C Compiler if requested
[[ -n $with_intel ]] && perl -pi -e "s@^USE_INTEL_COMPILER=.*@USE_INTEL_COMPILER=1@" config.inc

# Configure package path if requested
if [[ -n $package_dir ]] &&  perl -pi -e "s@^#PACKAGE_PREFIX=.*@PACKAGE_PREFIX=$package_dir@" config.inc

# Build and install XEmacs
nmake -f xemacs.mak
[[ -n $install ]] && nmake -f xemacs.mak install

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
