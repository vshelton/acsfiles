#!/usr/bin/env zsh

# Generate a cygwin setup kit in the directory cygwin-yyyy-mm-dd.
# Every package belonging to the Base category and all the packages
# listed in the extra_packages file will be included in the setup kit.
# All of the packages will be installed by default when setup.exe is run.
#
# Assumptions:
#  1. Cygwin is already installed on the machine.  At least coreutils,
#     gawk, grep, perl, rsync, tar and zsh must be installed.
#  2. A local repository of cygwin packages has been set up in the
#     directory identified by the CYGWIN_REPO environment variable
#     (defaults to the directory cygwin_repo).  The -s option
#     to this script can be used to create or sync up the repository.
#  3. The file extra_packages contains the list of packages to add
#     to the default list of cygwin packages.  Alternatively, if the
#     environment variable EXTRA_PACKAGES is set, it must be a list
#     of packages to add to the cygwin base install; e.g.
#       EXTRA_PACKAGES="bison flex" ./MakeCygwinKit
#     will add bison and flex to the setup kit.
#
# Once you have run this script, burn the contents of the
# cygwin-yyyy-mm-dd directory onto a CD, and that CD can be
# used to install all the specified cygwin packages, without
# requiring network access.

emulate zsh
setopt extended_glob
progname=${0:t}

function usage {
  print -u2 "\
usage: $progname \[-ahsvx\] \[-o outdir\]
       Create a cygwin setup kit into an output directory.  The default
       output directory is cygwin_disk-yyyy-mm-dd, but the directory
       can be changed through use of the -o option.  The contents
       of the output directory can then be burned onto a CD.
       All cygwin base packages are included in the generated kit.  The
       contents of the file \"extra_packages\" (or the environment variable
       \$EXTRA_PACKAGES) can be used to specify additional packages to add
       to the generated kit.  All of the specified packages will be installed
       by default when setup.exe is run.
       In order for this script to work, the directory identified
       by the environment variable \$CYGWIN_REPO (which has a default value
       of \"cygwin_repo\") must contain a mirror image of the cygwin package
       repository.  You can use the -s option to bootstrap this directory.
       Options:
       -a Copy the most recent arm-xscale crosscompiler tarball, (extracted
          from AOA's subversion repository) into the destination directory.
       -h Print this help message.
       -o outdir - specify the name of the output directory.
       -s Sync the packages with a cygwin mirror before running the script.
          The environment variable \$CYGWIN_SITE specifies which
          cygwin mirror to use.
       -v Print progress messages.
       -x Copy the latest XEmacs setup kits to the destination directory.
          The environment variable \$XEMACS_KIT_HOME specifies where to
          find the XEmacs setup kits."
}

# Parse the options
zparseopts -D a=copy_arm_tools h=help o:=output_opt s=sync_packages v=verbose x=copy_XEmacs_kits

if [[ -n $help ]]; then
  usage
  exit 0
fi

: ${CYGWIN_REPO:=$PWD/cygwin_repo}

# Add current directory to $PATH.
path=($PWD $path)

# Sync up to the latest packages.
if [[ -n $sync_packages ]]; then
  cd $CYGWIN_REPO
  # This command will display a list of mirror sites that support rsync:
  #  wget -q -O - http://cygwin.com/mirrors.txt | grep rsync | cut -d';' -f1 | sed -e 's;\(rsync://[0-9a-z.-]*\).*;\1;'
  CYGWIN_SITE=${CYGWIN_SITE:-rsync://mirrors.kernel.org/sourceware/cygwin/}
  [[ -n $verbose ]] && print -u2 "Syncing packages from cygwin mirror: $CYGWIN_SITE"
  rsync -az --exclude mail-archives $CYGWIN_SITE .
  cd -
fi

# Define the list of packages to be added to the base kit.
if [[ -n $EXTRA_PACKAGES ]]; then
  extra=( ${=EXTRA_PACKAGES} )
else
  [[ -r extra_packages ]] && extra=( $(<extra_packages) )
fi

# Create and change to the output directory.
if [[ -n $output_opt ]]; then
  outdir=$output_opt[2]
else
  outdir=cygwin_disk-$(date +'%Y-%m-%d')
fi
[[ -n $verbose ]] && print -u2 "Making a cygwin setup kit in $outdir"
if [[ ! -d $outdir ]]; then
  if ! mkdir $outdir ; then
    print -u2 "$progname: could not create $outdir."
    exit 1
  fi
fi
cp autorun.inf $outdir
cd $outdir

cp $CYGWIN_REPO/setup.exe $CYGWIN_REPO/setup.ini .

if [[ -n $verbose ]]; then
  if [[ -z $extra ]]; then
    print -u2 "Adding no extra packages to the base cygwin setup kit"
  else
    print -u2 "Adding these packages to the base cygwin setup kit: $extra"
  fi
fi

# Generate the setup.ini file.
MakeSetupIni.pl $extra

# Copy the packages into the release subdirectory.
[[ -n $verbose ]] && print -u2 "Copying the packages to $outdir"
tar -C $CYGWIN_REPO -f - -c $(grep '^install:' setup.ini | awk '{print $2}') | tar xf -

# Copy the arm-xscale cross-compiler kit.
if [[ -n $copy_arm_tools ]]; then
  [[ -n $verbose ]] && print -u2 "Copying arm crosstools to $outdir"
  svn export svn://cvs-srvr/tools/arm-xscale-cygwin-gcc.tar.gz >& /dev/null
fi

# Copy the most recent XEmacs setup kits.
if [[ -n $copy_XEmacs_kits ]]; then
  : ${XEMACS_KIT_HOME:=$HOME/software/XEmacsWindowsKit}
  [[ -n $verbose ]] && print -u2 "Copying latest XEmacs setup kits from $XEMACS_KIT_HOME to $outdir"
  cp -p $XEMACS_KIT_HOME/XEmacs_Setup_21.4*(om[1]) $XEMACS_KIT_HOME/XEmacs_Setup_21.5*(om[1]) .
fi

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
