#!/usr/bin/env zsh

# Build XEmacs versions (21.4 and 21.5, both native and cygwin).
# The native versions are not installed, but the cygwin versions
# are installed to /usr/local/xemacs-21.x-$(today).
# All kits reference packages installed under the cygwin path /XEmacs.

emulate -LR zsh
progname=${0:t}

function usage {
  print -u2 "\
usage: $progname [-h|--help] [--intel] [--libs] <21.4|21.5*>
       -h|--help            print this help message and exit
       --intel              use the intel compiler
       --libs               build the support libs (probably doesn't work)
       21.4                 build a 21.4 XEmacs kit from SCM
       21.5*                build some type of 21.5 XEmacs kit from SCM"
}

function make_shortcut {
  local args desc target xemacs_ver xemacs_date xemacs_type

  if ! cd $1 ; then
    print -u2 "$progname: could not open directory $1 to create a shortcut."
    return 1
  fi

  xemacs_ver=$2                # version (21.4 or 21.5-test, etc)
  xemacs_date=$3               # version date yyyy-mm-dd
  xemacs_type=$4               # type: "native", "cygwin", etc

  # In case you're wondering: yes, the handling of the target filename is bogus.
  case $xemacs_type in
  (*native*)
    target="$OLDPWD/xemacs-$xemacs_ver-$xemacs_date/src/xemacs.exe"
    args=
    ;;
  (*cygwin*)
    target="/bin/run.exe"
    if ! args="$(ls $USRLOCAL/xemacs-${xemacs_ver}-${xemacs_date}*/bin/xemacs-*.exe)" 2>/dev/null ; then
      print -u2 "$progname: could not find XEmacs target for link."
      cd -
      return 1
    fi
    args="--arguments=$args"
    ;;
  esac

  desc="XEmacs $xemacs_ver $xemacs_date ($xemacs_type)"
  mkshortcut --desc="$desc" --icon=/bin/run.exe --iconoffset=1 --name="$desc" --workingdir=$HOME $args $target
  cd -
}

zparseopts -D h=help -help=help -intel=with_intel -libs=build_libs

if [[ -n $help ]]; then
  usage
  exit 0
fi

# Optional '--libs' argument means to build the libraries.
# This probably doesn't work.
[[ -n $build_libs ]] && mk-xemacs-support-libs

[[ -n $with_intel ]] && with_intel="--intel"

# The default is to build both 21.4 and 21.5
(( $# == 0 )) && set 21.4 21.5

# The version is based on the date.
# Capture that information now in case we're crossing midnight.
VERSION=$(today)

# The location for the installed XEmacs.
: ${XEMACS_INSTALLED_ROOT:=/XEmacs}

# The directory where shortcuts are created.
: ${shortcut_dir:="$XEMACS_INSTALLED_ROOT/shortcuts"}

for ver in $*; do

  build_opts=
  install_default=
  case $ver in
  (21.4*)
    build_opts="--without-debug"
    ;;
  (21.5*)
    build_opts="--with-mule"
    install_default="t"
    ;;
  esac

  # Update to the latest SCM sources.
  scm_update xemacs-$ver

  # Make but don't install a native version.
  # We'll run it from the source directory.
  print "Building XEmacs $ver native"
  # Note that MakeNativeXEmacs requires the leading '-' in VERSION
  # because it uses GetSource to handle both tarballs and SCM versions.
  if VERSION="-$VERSION" MakeNativeXEmacs $build_opts --package-root="$(cygpath -m $XEMACS_INSTALLED_ROOT)" $with_intel $ver ; then
    make_shortcut $shortcut_dir $ver $VERSION "native"
  fi

  # Make and install a cygwin version and make it the default.
  print "Building XEmacs $ver cygwin"
  mkdir ../build >& /dev/null
  pushd ../build

  if MakeCygwinXEmacs xemacs-$ver-$VERSION $install_default ; then
    make_shortcut $shortcut_dir $ver $VERSION "cygwin"
  fi
  popd
done

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
