#!/usr/bin/env zsh

# Build native setup kits for XEmacs under Windows.
# usage: mk-XEmacsWindowsKit [-p] [21.4.xx] [21.5-xx]

# Assumptions:
# 1. I do not expect that this script will work for you right out of the box,
#    but perhaps with a small amount of tuning it will function in your setup.
# 2. Cygwin is installed on the build system.
# 3. InnoSetup is installed in e:/Program Files/Inno Setup 5 and the
#    pre-processor has been installed, too
#    (see http://www.jrsoftware.org/isdl.php#qsp for more details and
#    http://files.jrsoftware.org/ispack/ispack-5.1.6.exe for the setup kit).

# Set up a vanilla zsh environment
emulate -LR zsh
progname=${0:t}

# Things to customize for your system
: ${InnoSetup:="/e/Program Files/Inno Setup 5/iscc"}

# Update all the packages (delete them first)
PACKAGE_DIR=$(cygpath --mixed $PWD/packages)
if [[ $1 == '-p' ]]; then
  shift
  rm -rf $PACKAGE_DIR
  mkdir $PACKAGE_DIR
  cd $PACKAGE_DIR
  ../getPkgs
  cd -
fi

# Give both versions a consistent datestamp
export VERSION=$(date +'%Y-%m-%d')
INSTALL_DIR=$(cygpath --mixed $PWD/installed/$VERSION)

# If no arguments were supplied, build latest versions of 21.4 and 21.5
(( $# == 0 )) && set 21.4.21 21.5-b28

# Loop over the versions specified on the command line
for ver in $*; do
  kit_options=("/dKitname=-$VERSION")
  case $ver in
  (21.4*)
    branch=21.4
    branch_options="--without-debug"
    ;;
  (21.5*)
    branch=21.5
    branch_options="--with-mule"
    kit_options=($kit_options "/dMULE")
    ;;
  (*)
    print -u2 "$progname: unknown version specified: \"$ver\"."
    continue
    ;;
  esac

  # Make and install the executables
  MakeNativeXEmacs --install --prefix=$INSTALL_DIR $branch_options $branch
  if (( $? != 0 )); then
    print -u2 "$progname: \"MakeNativeXEmacs --install --prefix=$INSTALL_DIR $branch_options $branch\" failed."
    exit 1;
  fi

  # Run the Inno Setup compiler to make the kit
  $InnoSetup XEmacs.iss /dExecSrc=$INSTALL_DIR /dPkgSrc=$PACKAGE_DIR /dXEmacsVersion=$ver $kit_options

done

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
