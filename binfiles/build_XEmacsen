#!/usr/bin/env zsh

# Configure and build but don't install old debug copies of XEmacs 21.5
# under the $SRCROOT directory.

emulate -LR zsh
progname=${0:t}

# Clone the XEmacs 21.5 source hierarchy.
cd ${TMP:-/tmp}
project=xemacs-21.5
print -n "Cloning $project repository ... "
hg clone $SCMROOT/$project >/dev/null
print done
SCMROOT=$PWD
cd ./$project             # Necessary for "hg update" below.

# Here's how to generate a list of dates from hg history:
# hg history --template "{date|isodate}\n" | col 1 | sort -ur

# Define the default list of dates we will use
# for building old versions of XEmacs 21.5.
(( # == 0 )) &&
  set 2009-01-01 2009-07-01 2010-01-01 2010-07-05 2011-01-01 2011-04-02 \
      2011-07-03 2011-10-02 2012-01-01 2012-04-07 2012-07-02 2012-10-15

: ${USRLOCAL:/usr/local}

# Define a set of options to build a consistent and somewhat minimal XEmacs.
: ${CFLAGS:="-g -Os -m64 -msse4.2 -ffast-math -fno-exceptions"}
cfg_options=( --prefix=$USRLOCAL
              --with-cflags="$CFLAGS"
              --with-site-prefixes=$USRLOCAL
              --with-infopath=$USRLOCAL/info
              --disable-dialogs
              --disable-widgets
              --with-sound=native
              --with-package-path=$USRLOCAL/site-packages::$USRLOCAL/xemacs-packages
              --enable-pdump
              --enable-bignum=gmp
              --with-default-eol-detection
              --enable-mule
              --without-xim )
for d; do
  echo Building $project $d
  hg update -d $d
  pushd $(mk-src $project $d) > /dev/null
  ./configure $cfg_options >& conf.out
  make >& mk.out
  make check >& check.out
  popd
done

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
