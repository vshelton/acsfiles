#!/usr/bin/env zsh

# Run 'make check' on the version of zsh currently running

emulate -LR zsh
0=${0:t}

: ${USRLOCAL:/usr/local}
: ${BLDROOT:/usr/local/build}

version=${MODULE_PATH:h:h:h:t}
build_dir=$BLDROOT/$version
cd $build_dir || exit $?
if [[ ! ${MODULE_PATH:h:h:h:t} == *$(today)* ]]; then
  echoti smso
  print -u2 "\n$0: warning - testing a build that was not made today.\n"
  echoti rmso
  echoti flash
fi

# Run tests
outfile=/tmp/zsh_check$$.out
make check |& tee $outfile
err=${pipestatus[1]}
if (( err != 0 )); then
  print -u2 "$0: make check failed with error code $err.  See $outfile."
  exit $err
fi

# Summarize the results
print "\n${PWD:t}"
summarize_zsh_tests $outfile

rm -f $outfile /tmp/zsh.ztst.*(N)

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
