#!/usr/bin/env zsh

# Compare XEmacs build results between two builds
# Usage: diff_xemacs_build_results [-h] [-d] [-m] [-4] [-5] date1 [date2]
#            -h - Print this help message
#            -d - Compare debug builds
#            -m - Compare mule builds
#            -4 - Compare 21.4 builds
#            -5 - Compare 21.5 builds (this is the default)

emulate -LR zsh
progname=${0:t}

usage()
{
  print -u2 "\
usage: $progname [-h] [-d] [-m] [-4] [-5] date1 [date2]
       -h - Print this help message
       -d - Compare debug builds
       -m - Compare mule builds
       -4 - Compare 21.4 builds
       -5 - Compare 21.5 builds (this is the default)"
}

zparseopts -D h=help m=mule 4=stable 5=beta d=debug

if [[ -n $help ]]; then
  usage
  exit 0
fi

if [[ $# == 0 ]]; then
  usage
  exit 1
fi

: ${BLDROOT:=/usr/local/build}

date1=$1
date2=$(today)
(( $# > 1 )) && date2=$2

proj="xemacs-21.5"
[[ -n $stable ]] && proj="xemacs-21.4"
[[ -n $beta ]] && proj="xemacs-21.5"

build_type=
[[ -n $mule ]] && build_type="-mule"
[[ -n $debug ]] && build_type="-debug"

for f in Installation config.status; do
  diffs -s/$date1/$date2/g $BLDROOT/$proj-{$date1,$date2}$build_type/$f
done

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
