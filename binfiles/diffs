#!/usr/bin/env zsh

# Compare the two files, optionally making substitions to each file first.
# Usage: diff-subst [-h] [-s/regex1/regex2/[g]] file1 [-s/regex3/regex4/[g]] file2
#        -h - Print this help message
#        -s - Run perl substitition on file before comparing"

emulate -LR zsh
progname=${0:t}

usage()
{
  print -u2 "\
usage: $progname [-h] [-s/regex1/regex2/[g]] file1 [-s/regex3/regex4/[g]] file2
       -h - Print this help message
       -s - Run perl substitition on file before comparing"
}

if [[ $1 == "-h" ]]; then
  usage
  exit 0
fi

# createFile destFile [-s/regex1/regex2/[g]] srcFile
# returns $srcFile $shiftCount
#
# Create a possibly modified file
createFile()
{
  local destFile=$1     # Hack: this variable is not included in shiftCount
  shift                 #       nor checked in the argument list

  local pat=
  srcFile=              # Return value with external scope
  shiftCount=1          # Return value with external scope
                        # Default value of 1 assumes srcFile is specified

  # Grab optional substitution pattern and name of source file
  if [[ $1 == -s* ]]; then
    pat=${1#-}
    shift
    (( ++shiftCount ))
  fi
  if (( $# < 1 )); then
    usage
    exit 1
  fi
  srcFile=$1
  if [[ ! -e $srcFile ]]; then
    print -u2 "$progname: $srcFile does not exist".
    exit 2
  fi

  # Create (potentially modified) file
  if [[ -n $pat ]]; then
    perl -p -e $pat <$srcFile >$destFile
  else
    cp $srcFile $destFile
  fi
}

# Erase temporary files on interruption or exit
diff1=$(mktemp)
diff2=$(mktemp)
trap 'trap -; rm -f $diff1 $diff2' EXIT INT

integer shiftCount=0
srcFile=

createFile $diff1 "$@"
shift $shiftCount
split1=( ${(s:/:)srcFile} )     # Split source path into components

createFile $diff2 "$@"
shift $shiftCount
split2=( ${(s:/:)srcFile} )     # Split source path into components

# Remove common prefixes from file labels
integer i=1
while [[ i -le $#split2 && $split2[i] == $split1[i] ]]
do
  (( ++i > $#split1 )) && break
done

diff --unified=0 --label ${(j:/:)split1[i,-1]} $diff1 --label ${(j:/:)split2[i,-1]} $diff2

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
