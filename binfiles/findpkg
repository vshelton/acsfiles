#!/usr/bin/env zsh
#
# Report the filename of the most recent tarball
# matching the specified package name
#
# Usage: findpkg generic_package_name
# i.e. "findpkg xemacs" ==> "/c/acs/software/zip/xemacs-21.4.22.tar.bz2"

emulate -LR zsh
setopt no_case_glob
progname=${0:t}

if (( $# != 1 )); then
  print -u2 "Usage: $progname <pkg>"
  print -u2 "       pkg is a stripped package name, like \"gcc\" or \"make\"."
  exit 1
fi

pkgbase=$1

# Most unix-like package names have a dash separating the package name
# from the version, but tcl and tk do not, so support those, too.
pkgs=( $ZIPROOT/$pkgbase{-,}*.(gz|tgz|bz2|xz|lzma|tar)(Nom) )
if [[ ! -e $pkgs[1] ]]; then
  print -u2 progname: no files with the prefix of \"$pkgbase\" could be found in $ZIPROOT.
  exit 1
fi

if [[ ! -r $pkgs[1] ]]; then
  print -u2 $progname: $pkgs[1] is unreadable.
  exit 1
fi

print $pkgs[1]

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
