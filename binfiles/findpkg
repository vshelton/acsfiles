#!/usr/bin/env zsh
#
# Report the filename of the most recent tarball
# matching the specified package name
#
# Usage: findpkg generic_package_name

emulate -LR zsh
progname=${0:t}

if (( $# != 1 )); then
  print -u2 "Usage: $progname <pkg>"
  print -u2 "\tpkg is a stripped package name, like \"gcc\" or \"make\"."
  exit 1
fi
pkgbase=$1

# Equivalent to: pkgname=$(ls -t $ZIPROOT/pkgbase*(gz|tgz|bz2) | head -1)
pkgname=$(print $ZIPROOT/$pkgbase-*.(gz|tgz|bz2)(om[1])) 2>/dev/null
if [[ ! -e $pkgname ]]; then
  # Try with no hyphen for tcl and tk
  pkgname=$(print $ZIPROOT/$pkgbase*.(gz|tgz|bz2)(om[1])) 2>/dev/null
  if [[ ! -e $pkgname ]]; then
    print -u2 progname: no files with the prefix of \"$pkgbase-\" or \"$pkgbase\" could be found in $ZIPROOT.
    exit 1
  fi
fi

if [[ ! -r $pkgname ]]; then
  print -u2 $progname: $pkgname is unreadable.
  exit 1
fi

print $pkgname

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
