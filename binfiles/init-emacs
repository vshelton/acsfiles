#!/usr/bin/env zsh

emulate -LR zsh

# Clone a repo, build and install it -
# works for conky, emacs and zsh.

: ${USRLOCAL:=/usr/local}
: ${SCMROOT:=$HOME/scmroot}

#
# Utility functions.
#

# Define a function to clone a git repo.
function clone_git_repo() {
  # $1 - name of git repo (i.e. emacs)
  # $2 - URL of repo
  local nickname=$1
  local repo_url=$2

  (
    mkdir -p ${SCMROOT}/gitroot
    cd ${SCMROOT}/gitroot
    git clone ${repo_url} ${nickname}
    touch --date=2001-01-01 ${nickname}/last_update
    cd ..
    ln -s gitroot/${nickname}
  )
}

# Define a function to link the most recent version
# of an application into the ${USRLOCAL} hierarchy.
function link_latest() {
  # $1 - destination name, i.e. "emacs"
  # $2 - source name, i.e. "emacs-2016-01-02"
  local nickname=$1
  local fullname=$2
  (
    cd ${USRLOCAL}
    ln -s ${fullname} ${nickname}
    cd bin
    ln -s ../${nickname}/bin/* .
  )
}

# Define a (debian) function to list a package name.
function list_package() {
  [[ -x /usr/bin/apt-cache ]] || return 1
  apt-cache search "$1" | sed -sne 's/ .*//p'
}

#
# Code starts here.
#

for d in $SCMROOT/acsfiles/rcfiles/lib/zsh-functions $HOME/lib/zsh-functions $HOME; do
  if [[ -e $d/package_manager_functions ]]; then
    . $d/package_manager_functions
    break
  fi
done

# Figure out which app we're building based on the invocation name.
case $0 in
(*conky*)
  nickname=conky
  build_script=mk-software conky
  ;;
(*emacs*)
  nickname=emacs
  build_script="mk-emacs --with-x-toolkit=lucid"
  ;;
(*exa*)
  nickname=exa
  build_script="mk-software exa --snapshot --install"
  ;;
(*i3*)
  nickname=i3
  build_script="mk-software i3 --snapshot --install"
  ;;
(*tilda*)
  nickname=tilda
  build_script="mk-software tilda --snapshot --install"
  ;;
(*zsh*)
  nickname=zsh
  build_script="mk-software zsh --snapshot --install"
  ;;
(*)
  print -u2 "Only conky, emacs, tilda and zsh are currently supported."
  exit 1;
  ;;
esac

# Define the source code repositories.
typeset -A repos
repos[conky]="https://github.com/brndnmtthws/conky.git"
repos[emacs]="git://git.savannah.gnu.org/emacs.git"
repos[exa]="https://github.com/ogham/exa.git"
repos[i3]="https://github.com/i3/i3.git"
repos[tilda]="https://github.com/lanoxx/tilda.git"
repos[zsh]="git://git.code.sf.net/p/zsh/code"

# Define the packages needed.  The names depend on whether
# the package manager is based on rpm or apt-get.
typeset -A deps

if suse-p; then
  deps[conky]="cmake gcc-c++ freetype2-devel lua-devel libXdamage-devel      \
               ncurses-devel libXinerama-devel libXft-devel"
  deps[emacs]="autoconf automake giflib-devel gnutls-devel libXaw-devel      \
               libjpeg-devel libpng-devel libtiff-devel makeinfo"
  deps[exa]="cargo cmake rustc"
  deps[tilda]="clang libconfuse-dev libgtk-3-dev libvte-2.91-dev llvm"
  deps[zsh]="autoconf gcc pcre-devel ncurses-devel makeinfo yodl"

elif arch-p; then
  deps[conky]=cmake
  deps[emacs]="autoconf automake giflib gnutls libxaw   \
               libjpeg-turbo libtiff texinfo"
  deps[exa]="cargo cmake rustc"
  deps[tilda]="clang libconfuse libgtk-3 libvte-2.91 llvm"
  deps[zsh]="autoconf gcc pcre-devel ncurses-devel texinfo yodl"

elif debian-p; then
  # On debian, at some point the package name changed from libXdamage-dev
  # to libxdamage-dev, so we will special-case it here.
  deps[conky]="cmake g++ libfreetype6-dev liblua5.2-dev         \
               $(list_package 'lib.damage-dev')                 \
               libncurses5-dev libxft-dev libxinerama-dev make"

  # Figure out which turbo development library
  deps[emacs]="autoconf automake libgif-dev libgnutls28-dev      \
               $(list_package 'jpeg.*turbo-dev')                 \
               libtiff5-dev libxaw7-dev libxpm-dev make texinfo"
  deps[exa]="cargo cmake rustc"
  deps[i3]="gir1.2-pango-1.0 libpango-1.0-0 libpango-perl libpango1.0-0     \
            libpango1.0-dev libpangocairo-1.0-0 libpangoft2-1.0-0           \
            libstartup-notification0-dev libstartup-notification0           \
            libx11-xcb-dev libxcb-composite0-dev libxcb-cursor-dev          \
            libxcb-damage0-dev libxcb-dpms0-dev libxcb-dri2-0-dev           \
            libxcb-dri3-dev libxcb-glx0-dev libxcb-icccm4-dev libxcb-icccm4 \
            libxcb-image0-dev libxcb-keysyms1-dev libxcb-present-dev        \
            libxcb-randr0-dev libxcb-record0-dev libxcb-render-util0-dev    \
            libxcb-render0-dev libxcb-shape0-dev libxcb-shm0-dev            \
            libxcb-sync-dev libxcb-util0-dev libxcb-xfixes0-dev             \
            libxcb-xinerama0-dev libxcb-xkb-dev libxcb-xrm-dev              \
            libxcb1-dev libxkbcommon-dev libxkbcommon-x11-dev               \
            libxkbfile-dev"
  deps[tilda]="clang libconfuse-dev libgtk-3-dev libvte-2.91-dev llvm"
  deps[zsh]="autoconf gcc libncurses5-dev libpcre3-dev make texinfo yodl"

# Define the packages needed on a Fedora-based VM.
elif fedora-p ; then
  deps[conky]="conky conky-manager cmake lua-devel gcc-c++ freetype-devel    \
               libXdamage-devel libXext-devel libXft-devel libXinerama-devel \
               ncurses-devel"
  deps[emacs]="autoconf automake giflib-devel gnutls-devel libXaw-devel      \
               libjpeg-turbo-devel libtiff-devel texinfo"
  deps[exa]="cargo cmake rustc"
  deps[tilda]="clang libconfuse-dev libgtk-3-dev libvte-2.91-dev llvm"
  deps[zsh]="ftp://ftp.pbone.net/mirror/ftp5.gwdg.de/pub/opensuse/repositories/Publishing/openSUSE_Factory/x86_64/yodl-3.06.00-2777.25.x86_64.rpm \
             autoconf gcc pcre-devel ncurses-devel texinfo"
fi

# Clone the source code repository.
clone_git_repo ${nickname} ${repos[${nickname}]}

# Install the pre-requisite packages.
install_package ${=deps[${nickname}]}

# Build the package.
${=build_script}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
