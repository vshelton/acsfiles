#!/usr/bin/env zsh
#
# Report the directory name of the source directory of the
# most recent version of the specified package
#
# Usage: latest generic_package_name

emulate zsh
progname=${0:t}

if [[ $# -ne 1 ]]; then
  print -u2 "Usage: $progname <pkg>"
  print -u2 "\tpkg is a stripped package name, like \"gcc\" or \"make\"."
  exit 1
fi
pkgbase=$1

# Equivalent to: pkgname=$(ls -t $SRCROOT/pkgbase* | head -1)
pkgname=$(print $SRCROOT/$pkgbase-*(om[1])) 2>/dev/null
if [[ ! -e $pkgname ]]; then
  # Try with no hyphen for tcl and tk
  pkgname=$(print $SRCROOT/$pkgbase*(om[1])) 2>/dev/null
  if [[ ! -e $pkgname ]]; then
    print -u2 "$progname: no files with the prefix of \"$pkgbase-\" or \"$pkgbase\" could be found in $SRCROOT."
    exit 1
  fi
fi

if [[ ! -r $pkgname ]]; then
  print -u2 "$progname: $pkgname is unreadable."
  exit 1
fi

print $pkgname

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
