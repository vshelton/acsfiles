#!/usr/bin/env zsh
#
# Report the directory name of the source directory of the
# most recent version of the specified package
#
# Usage: latest generic_package_name

emulate -LR zsh
setopt nonomatch
progname=${0:t}

if (( $# != 1 )); then
  print -u2 "\
usage: $progname <pkg>
       pkg is a bare package name, like \"gcc\" or \"make\"."
  exit 1
fi
pkgbase=$1

pkgs=( $SRCROOT/${pkgbase}-*(om) )
if [[ ! -e $pkgs[1] ]]; then
  # Try with no hyphen for tcl and tk
  pkgs=( $SRCROOT/${pkgbase}*(om) )
  if [[ ! -e $pkgs[1] ]]; then
    print -u2 "$progname: no files with the prefix of \"$pkgbase-\" or \"$pkgbase\" could be found in $SRCROOT."
    exit 1
  fi
fi

if [[ ! -r $pkgs[1] ]]; then
  print -u2 "$progname: $pkgname is unreadable."
  exit 1
fi

print $pkgs[1]

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
