#!/usr/bin/env zsh

# Loop a command several times until the command passes,
# or until a maximum number of loops has expired.

emulate -LR zsh
progname=${0:t}

: ${maxloops:=25}
: ${sleep_period:=10}
: ${errfile:=/tmp/$(whoami)-$progname.$$}

# Run the command once with no redirection so the user knows what's happening
$*
retcode=$?

# Repeatedly try the command and save the output
for (( i=1; retcode != 0 && i < maxloops; i++ ))
do
  sleep $sleep_period
  $* >>& $errfile
  retcode=$?
done

if (( i == maxloops )); then
  print -u2 "$progname: $* failed $maxloops times."
  if [[ -s $errfile ]]; then
    print -u2 "See $errfile for details."
  else
    rm -f $errfile
  fi
  exit 1
fi

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
