#!/usr/bin/env zsh

# Make a source tree from the SCM-based sources of the specified project.

emulate -LR zsh
progname="${0:t}"

case $# in
(1)
  project="$1"
  ;;
(2)
  project="$1"
  VERSION="$2"
  ;;
(*)
  print -u2 "usage: $progname <project> [version]"
  exit 1
  ;;
esac

# If no other version name was supplied, the default
# version stamp is based on the current date.
: ${VERSION:=$(today)}

: ${USRLOCAL:=/usr/local}
: ${SRCROOT:=$USRLOCAL/src}
target_dir=$SRCROOT/$project-$VERSION

: ${SCMROOT:=~/scmroot}
project_scm_dir=$SCMROOT/$project
if [[ ! -d $project_scm_dir ]]; then
  print -u2 "$progname: there is no accessible SCM hierarchy at $project_scm_dir."
  exit 2
fi

mkdir -p $target_dir

tar cf - -C $project_scm_dir --exclude={CVS,.hg,.svn} . | tar xf - -C $target_dir

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
