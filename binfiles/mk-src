#!/usr/bin/env zsh

# Make a source tree from the CVS sources of the specified project.

emulate -LR zsh

set -e		# Exit at the first sign of trouble.

cmdname="${0:t}"

case $# in
  (1)	PROJECT="$1"
	;;
  (2)	PROJECT="$1"
	VERSION="$2"
	;;
  (0|*)	print -u2 "Usage: $cmdname <project> [version]"
	exit 1
	;;
esac

# If no other version name was supplied, the default
# version stamp is based on the current date.
: ${VERSION:=$(today)}

TARGET="$PROJECT-$VERSION"

: ${USRLOCAL:=/usr/local} ${SRCROOT:=$USRLOCAL/src}
SRCDIR="${SRCDIR:-$SRCROOT/$TARGET}"

: ${CVSROOT:=$HOME/cvsroot}
CVSROOT=$CVSROOT/$PROJECT
if [[ ! -d "$CVSROOT" ]]; then
  print -u2 "There is no CVS hierarchy at $CVSROOT."
  exit 2
fi

mkdir -p "$SRCDIR"
cd "$SRCDIR"

# De-reference symlinks when creating source trees.
# I put this in because emacs now uses the source files
# regex.c and regex.h from the gnulib project
tar cf - -C "$CVSROOT" --exclude CVS --dereference . | tar xf -

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
