#!/usr/bin/env zsh

# Make a minimal XEmacs suitable for debugging
 
emulate -LR zsh
setopt shwordsplit              # For CFLAGS
progname=${0:t}

: ${CC:=gcc}
: ${CFLAGS:="-g -O"}
: ${VERSION:=min}
export CC CFLAGS VERSION

CFG_OPTS=( $argv )
for (( i=$#CFG_OPTS; i > 0; --i )); do
  [[ ${CFG_OPTS[$i]} != --* ]] && CFG_OPTS[$i]=()
done
shift $#CFG_OPTS

: ${XEMACS:=xemacs-21.5}
XEMACS=${1:-$XEMACS}

if (( $#CFG_OPTS == 0 )); then
  CFG_OPTS=( without-x with-sound=no with-database=no without-ldap without-gpm )
  case $XEMACS in
  (*21.4*)
    ;;
  (*)
    CFG_OPTS[$#CFG_OPTS+1]="disable-kkcc"
    CFG_OPTS[$#CFG_OPTS+1]="with-mule"
    ;;
  esac
  CFG_OPTS=( --${^CFG_OPTS} )
fi

#echo TARGET= $XEMACS CFG_OPTS = $CFG_OPTS

source_dir=$(mk-src $XEMACS)
if (( $? != 0 )); then
  print -u2 "mk-src $XEMACS failed"
  exit 1
fi

cd $source_dir
./configure $CFG_OPTS >& conf.out
if (( $? != 0 )); then
  print -u2 "configure failed; see $PWD/conf.out"
  exit 1
fi

make >& mk.out
if (( $? != 0 )); then
  print -u2 "make failed; see $PWD/mk.out"
  exit 1
fi

make check >& check.out

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
