#!/usr/bin/env zsh

# Make a minimal XEmacs suitable for debugging

emulate -LR zsh
setopt shwordsplit                      # For CFLAGS
progname=${0:t}

: ${CC:=gcc}
: ${CFLAGS:="-g -O"}
: ${VERSION:=min}
export CC CFLAGS VERSION

CFG_OPTS=( $argv )
for (( i=$#CFG_OPTS; i > 0; --i )); do
  [[ ${CFG_OPTS[$i]} == --* ]] && break # Stop on last option
  CFG_OPTS[$i]=()                       # Remove trailing non-option args
done
shift $#CFG_OPTS

: ${XEMACS:=xemacs-21.5}
XEMACS=${1:-$XEMACS}

if (( $#CFG_OPTS == 0 )); then
  CFG_OPTS=( without-x with-sound=no with-database=no without-ldap without-gpm )
  case $XEMACS in
  (*21.4*)
    ;;
  (*)
    CFG_OPTS[$#CFG_OPTS+1]="disable-kkcc"
    CFG_OPTS[$#CFG_OPTS+1]="with-mule"
    ;;
  esac
  CFG_OPTS=( --${^CFG_OPTS} )           # Showing off rc-style expansion
fi

#echo TARGET= $XEMACS CFG_OPTS = $CFG_OPTS

if ! source_dir=$(mk-src $XEMACS); then
  print -u2 "mk-src $XEMACS failed"
  exit 1
fi

cd $source_dir
if ! ./configure $CFG_OPTS >& conf.out; then
  print -u2 "configure failed; see $PWD/conf.out"
  exit 1
fi

if ! make >& mk.out; then
  print -u2 "make failed; see $PWD/mk.out"
  exit 1
fi

make check >& check.out

# Make a symlink to point to the local package directory
ln -s ../../xemacs-packages

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
