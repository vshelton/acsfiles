#!/usr/bin/env zsh

# Make the latest installed version of XEmacs be the default
# version of XEmacs for the system.

# usage: new_XEmacs [-v] [version]
#     -v  operate in verbose mode
#   version must be either 21.4 or 21.5; it defaults to 21.4.

emulate -LR zsh
setopt extendedglob

progname=${0:t}
0=$progname                             # for error messages from builtins

# Remove an old link and link to the directory
# that matches the specified pattern.
# $1 = matched pattern
# $2 = link name
function newlink {
  local target
  [[ -n $verbose ]] && command ls -ld $PWD/$2
  rm -f $2

  # Link to the directory which matches the specified pattern.
  target=$(print $1)
  [[ -d $target ]] && ln -s $target $2

  [[ -n $verbose ]] && command ls -ld $PWD/$2
}

function usage {
  print -u2 "\
usage: $progname [-v] [version]
       Make the most-recently installed version of XEmacs the default version
       on the system.  This command updates the 'xemacs', 'xemacs-debug' and
       'xemacs-mule' commands.
       The option '-v' makes $progname operate verbosely.
       Version must be either 21.4 or 21.5; if a version is not specified
       explicitly, the new link is created based on whatever version was
       linked previously ."
}

zparseopts -D h=help v=verbose

if [[ -n $help ]]; then
  usage
  exit 0
fi

# Find the directory where the symlink to xemacs lives.
ipath=$(find_install_root xemacs)
if [[ ! -d $ipath ]]; then
  print -u2 "$progname: install root $ipath not found"
  exit 2
fi
cd $ipath/.. || exit $?

# Ascertain whether to link to the latest 21.4 or 21.5
# based on what the previous link was
ver=21.4
[[ $ipath == *21.5* ]] && ver=21.5

# Allow the user to specify 
(( $# > 0 )) && ver=$1
if [[ $ver != 21.[45] ]]; then
  print -u2 "$progname: 21.4 and 21.5 are the only versions supported."
  exit 1
fi

# 'xemacs' has a funky pattern because we need to exclude
# both mule and debug versions
newlink xemacs-${ver}*~*-mule~*-debug(oc[1]) xemacs

newlink xemacs-${ver}*-debug(oc[1]) xemacs-debug
newlink xemacs-${ver}*-mule(oc[1]) xemacs-mule

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
