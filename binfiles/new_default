#!/usr/bin/env zsh

# Make the latest installed version of some package be the default

# usage: new_default <pkg> [directory]
# e.g. new_default zsh
# If no directory is specified, the parent of the install prefix
# (ie, $prefix/.. in autoconf terms) is assumed.
# See find_install_root for details.

emulate -LR zsh
progname=${0:t}
0=$progname                             # for error messages from builtins

function usage {
  print -u2 "usage: $progname [-v] <pkg> [directory]"
}

zparseopts -D h=help v=verbose

if [[ -n $help ]]; then
  usage
  exit 0
fi

if [[ $# != 1 ]] && [[ $# != 2 ]]; then
  usage
  exit 1
fi

ipath=${2:-$(find_install_root $1)/..}
if [[ ! -d $ipath ]]; then
  print -u2 "$progname: install root $ipath not found"
  exit 2
fi
cd $ipath || exit $?

[[ -n $verbose ]] && command ls -ld $PWD/$1
link_latest $1
[[ -n $verbose ]] && command ls -ld $PWD/$1

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
