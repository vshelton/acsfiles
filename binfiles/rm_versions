#!/usr/bin/env zsh

# Allow the user to select and remove versions of src, build and
# install kits for specified packages

emulate -LR zsh
setopt extendedglob

progname=${0:t}

function usage {
  print -u2 "usage: $progname [-i] [-n] [-v] pkg1 [pgg2 [pgk3...]]"
}

zparseopts -D h=help i=interactive n=printonly v=verbose

if [[ -n $help ]]; then
  usage
  exit 0
fi

# List kits and allow user to choose interactively
if [[ -n $interactive ]]; then
  buildkits=($(all_versions $*))
  [[ -z $buildkits ]] && exit 0
  removable=
  PROMPT3="Select buildkit(s) to remove: "
  select dir in "Remove kits" "Cancel" $buildkits
  do
    [[ $dir == Cancel ]] && exit 0
    [[ $dir == "Remove kits" ]] && break
    removable=($removable $dir)
  done

# Not interactive, so generate the list of kits
# without user intervention
else
  removable=($(all_versions $*))
fi
[[ -z $removable ]] && exit 0

if [[ -n $printonly || -n $verbose ]]; then
  echo rm -rf ${^$(all_dirs)}/$^removable(N^M)
fi

if [[ -z $printonly ]]; then
  rm -rf ${^$(all_dirs)}/$^removable(N^M)
fi

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
