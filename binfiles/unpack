#!/usr/bin/env zsh
#
# Unpack a tarball into the source directory.
#
# Usage: unpack generic_package_name

emulate -LR zsh

function cmdError {
  cmd_status=$?
  (( $cmd_status == 0 )) && return 1
  return 0
}

progname=${0:t}

if (( $# != 1 )); then
  print -u2 "Usage: $progname <pkg>"
  print -u2 "\tpkg is a stripped package name, like \"gcc\" or \"make\"."
  exit 1
fi
pkgbase=$1

pushd $SRCROOT >& /dev/null
if cmdError; then
  print -u2 $progname: the root of the source hierarchy, $SRCROOT, does not exist.
  exit $cmd_status
fi

pkgfile=$(findpkg $pkgbase)
if cmdError; then
  # Error already reported
  exit $cmd_status
fi

# This is much easier now that tar knows how to figure
# out what uncompressor to use
tar xf $pkgfile
if cmdError; then
  print -u2 $progname: error unpacking $pkgfile
  exit $cmd_status
fi

# Print the package name before leaving
print $PWD/${${${pkgfile:t}%.tar*}%.tgz}

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
