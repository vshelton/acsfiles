#!/usr/bin/env zsh

# Update the local sources for a specified project and
# if any new files are found, run a full build.
# Syntax: update_and_build <project_name> <build_script> [build_options]
# Assumptions:
#   1. An SCM tree for the specified project exists at
#      $SCMROOT/<project_name>
#   2. A build tree exists at $BLDROOT

emulate -LR zsh
progname=${0:t}

case $# in
(0|1)
  print -u2 "usage: $progname <project_name> <build_script> [build_options]"
  exit 1
  ;;
esac

: ${SCMROOT:=~/scmroot}

project_name=$1; shift
build_script=$1; shift
update_file="last_update"

if scm_update $project_name ; then
  cd $SCMROOT/$project_name

  if (( $(newer_than $update_file) > 0 )); then
    (
      cd $BLDROOT
      print "\nBuilding $project_name"
      $build_script $*
    )
    # touch update_file if build script succeeded
    (( $? == 0 )) && touch $update_file
  fi
fi

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
