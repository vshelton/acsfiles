
# Order of execution for zsh startup:
# 1. .zshenv - Always, unless -f is specified
# 2. .zprofile - Only if login shell
# 3. .zshrc - Only for interactive shells
# 4. .zlogin - Only if login shell

# Set up a common environment; typically this will
# be common for all the nodes within a network.
# USRLOCAL is the destination hierarchy for locally-built packages,
# i.e. /usr/local or /opt.
# SRCROOT is the root of source hierarchy for locally-built packages.
# BLDROOT is the root of the build hierarchy for locally-built packages.
# ZIPROOT is the directory where tarfiles live.
: ${USRLOCAL:=/opt}
: ${SRCROOT:=$USRLOCAL/src}
: ${BLDROOT:=$USRLOCAL/build}
: ${ZIPROOT:=$USRLOCAL/zip}
: ${SCMROOT=~/scmroot}
export USRLOCAL SRCROOT BLDROOT ZIPROOT SCMROOT

export LC_COLLATE=C
#unset LANG
#unset LC_TIME

cdpath=($BLDROOT $SRCROOT ${BLDROOT:h} ${SRCROOT:h} $USRLOCAL ~ ..)

SAVEHIST=$HISTSIZE
HISTFILE=~/.zhistory

path=($ZDOTDIR/bin $USRLOCAL/bin /usr/bin /bin /sbin /usr/sbin /etc)

# Invoke system specific customizations
system_specific .zshenv -post

# Clean up path: remove trailing slashes and non-existant directories
# and resolve symbolic links
path=($^path(N))
path=(${^path%%/##})
foreach p in $path; do
  [[ -L $p ]] && path[(r)$p]=$(cd $p; pwd -r)
done

system_specific .zlogin

# Set manpath based on path, but include /usr/share/man.
manpath=(/usr/share/man)
for p in $path
do
  m=${p:s/bin/man/}
  [[ $m != $p ]] && [[ -d $m ]] && manpath=( $manpath $m )
done

# Clean up manpath: remove trailing slashes and non-existant directories
# and resolve symbolic links
manpath=($^manpath(N))
manpath=(${^manpath%%/##})
for (( i=1; i < $#manpath+1; i++ )); do
  [[ -L $manpath[$i] ]] && path[$i]=$(abspath $path[$i])
done

if [[ ! -e ${ZDOTDIR:-$HOME}/.zlogin.${HOST%%.*} ]]; then
  print -u2 "\n\n\nWarning\!\!\!\nNo ${ZDOTDIR:-$HOME}/.zlogin.${HOST%%.*} script exists\!\nThe environment has not been initialized correctly\!\!\!\n\n\n"
fi

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
