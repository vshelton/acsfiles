# Set up aliases for interactive shells.

# Possible file listers in priority order.
l=( exa ls )               

# The 'LS' environment variable has priority.
if [[ -z $LS ]]; then
  for f in $l; do
    LS=$(whence $f) && break
  done
fi

# /bin/ls coreutils now supports dereferencing symlinks to directories!
if $LS --dereference-command-line-symlink-to-dir /dev/null >& /dev/null; then
  alias ls="$LS --dereference-command-line-symlink-to-dir"

else

  # Dereference symbolic links in argv pointing to directories.
  # Use 'eval' in order to use the value of LS as currently
  # defined, not as defined at run-time.
  eval "function ls() {
    # Split out options.
    opts=()
    for a; do
      [[ \"\$a\" != -* ]] && break
      opts+=\"\$a\"
      shift
    done
    #echo opts = \$#opts \"\$opts\" argv = \$#argv \"\$argv\"

    # Save the filenames of nonexistent files
    # so they don't get ignored in the glob below.
    bad=()
    for a; do
      [[ ! -e \"\$a\" ]] && bad+=\"\$a\"
    done
    #echo bad = \$#bad \"\$bad\"
    $LS \$opts \$bad \${^~argv}(N-M)
  }"
fi

# Define common aliases across all file listers.
alias l="ls --classify"
alias lr="ls -R"

# Define aliases for /bin/ls.
alias la="ls -A"
alias ll="ls -l --time-style=long-iso"
alias lt="ll -rt"

# (Re)define aliases for exa.
if [[ $LS == *exa ]]; then
  alias la="ls --all"
  alias ll="ls --long --bytes --time-style=long-iso"
  alias lt="ll --sort=modified"
fi

# Clean up.
unset l LS

alias -- +w="chmod u+w"
alias -- -w="chmod a-w"
alias -- +x="chmod u+x"
alias -- -x="chmod a-x"
alias all_versions="noglob all_versions"
alias aver=all_versions
alias d='dirs -v'
alias e='fcedit -q'
alias grep='command grep --extended-regexp --text'
alias h=history
alias m=$PAGER
alias new_xp='new_default -v xemacs-packages $USRLOCAL'
alias new_zsh='new_default -v zsh'
alias patch='command patch --version-control=numbered --backup'
alias pd=pushd
alias pp=popd
alias r1="noglob recentfiles 1"
alias r2="noglob recentfiles 2"
alias r3="noglob recentfiles 3"
alias r4="noglob recentfiles 4"
alias r5="noglob recentfiles 5"
alias r6="noglob recentfiles 6"
alias r7="noglob recentfiles 7"
alias r8="noglob recentfiles 8"
alias r9="noglob recentfiles 9"
alias rm_versions="noglob rm_versions"
alias rmver="rm_versions -v"
alias rr1="noglob recursivefiles 1"
alias rr2="noglob recursivefiles 2"
alias rr3="noglob recursivefiles 3"
alias rr4="noglob recursivefiles 4"
alias rr5="noglob recursivefiles 5"
alias rr6="noglob recursivefiles 6"
alias rr7="noglob recursivefiles 7"
alias rr8="noglob recursivefiles 8"
alias rr9="noglob recursivefiles 9"
alias tt='tar t -f'
alias ttv='tar tv -f'
alias tx='tar x -f'
alias txv='tar xv -f'
alias ve=vared

unalias run-help >&/dev/null
autoload -U run-help

# Use tkman where appropriate
[[ -n $DISPLAY ]] && whence tkmanclient >/dev/null && alias man=tkmanclient

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
