# Define shell behaviors for interactive shells.

# Remove duplicates from common path variables.
# Note the "-g", which is required because this
# script is being sourced through a function.
typeset -gU cdpath fpath manpath

cdpath=($BLDROOT $SRCROOT ${BLDROOT:h} ${SRCROOT:h} $USRLOCAL ~ ..)

typeset -g DIRSTACKSIZE=20 FIGNORE='~'
histchars=\!,#
typeset -g HISTSIZE=4096 WORDCHARS='.-_'
typeset -g SAVEHIST=$HISTSIZE HISTFILE=~/.zhistory

# Set manpath based on path, but include /usr/share/man.
manpath=( $path:gs/bin/man/ )
manpath[(r)/usr/man]="/usr/share/man"
manpath[(r)/etc]=''

# Clean up manpath: remove trailing slashes and non-existant directories
# and resolve symbolic links.
manpath=($^manpath(N))
manpath=(${^manpath%%/##})
for (( i=1; i < $#manpath+1; i++ )); do
  [[ -L $manpath[$i] ]] && manpath[$i]=$(abspath $manpath[$i])
done

# Capture core files
whence ulimit >/dev/null && ulimit -c unlimited

# Shell-specific behavior
setopt noalwayslastprompt
setopt appendhistory
setopt autolist
setopt automenu
setopt autoremoveslash
setopt extendedglob
setopt extendedhistory
setopt hashcmds
setopt hashdirs
setopt histignoredups
setopt histnostore
setopt interactivecomments
setopt kshoptionprint
setopt listtypes
setopt markdirs
setopt nohup
setopt noflowcontrol
setopt nolistbeep
setopt nonomatch
setopt notify
setopt nopromptsp 2>/dev/null
setopt pushdignoredups
setopt pushdsilent
setopt rmstarsilent

PROMPT="%2c %w %T%(#.#.); "
PROMPT="%B: $PROMPT%b"
PROMPT2="; "

if [[ $TERM != emacs ]]; then
  autoload -U promptinit
  promptinit
  prompt bart

  stty -parenb -istrip cs8 clocal erase '^?'
fi

# Save the session command history separate from .zhistory
acs_session_histfile=$HOME/.hist/$(date '+%Y-%m-%d-%H%M').${HOST%%.*}

# Local Variables:
# mode: shell-script
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
