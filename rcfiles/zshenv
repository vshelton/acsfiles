
# Order of execution for zsh startup:
# 1. .zshenv - Always, unless -f is specified
# 2. .zprofile - Only if login shell
# 3. .zshrc - Only for interactive shells
# 4. .zlogin - Only if login shell

# Remove duplicates from common path variables
typeset -U cdpath fpath manpath path

# in order to facilitate trying out new environments,
# we'll use ZDOTDIR instead of HOME from here on out.
: ${ZDOTDIR:=~}

# Optional tracing
# TRACE_ZSH_TIME=t TRACE_ZSH=t TRACE_ZSH_FILE=$HOME/ztrace.out zsh
[[ -n $TRACE_ZSH_TIME ]] && PS4='+[%D{%M:%S}]%N:%i> '
if [[ -n $TRACE_ZSH ]]; then
  [[ -n "$TRACE_ZSH_FILE" ]] && exec 2> "${TRACE_ZSH_FILE}"
  setopt xtrace
fi

# Define utility functions; some are used interactively and some
# are used within scripts, so it's best to define the whole lot here.
setopt extendedglob
f=$ZDOTDIR/lib/zsh-functions
if [[ -d $f ]]; then
  fpath=($f $fpath)
  autoload $(print $f/*~*~(:t))
fi

# Invoke system specific customizations
system_specific .zshenv -pre

# Set up a common environment; typically this will
# be common for all the nodes within a network.
# USRLOCAL is the destination hierarchy for locally-built packages,
# i.e. /usr/local or /opt.
# SRCROOT is the root of source hierarchy for locally-built packages.
# BLDROOT is the root of the build hierarchy for locally-built packages.
# ZIPROOT is the directory where tarfiles live.
: ${USRLOCAL:=/opt}
: ${SRCROOT:=$USRLOCAL/src}
: ${BLDROOT:=$USRLOCAL/build}
: ${ZIPROOT:=$USRLOCAL/zip}
: ${SCMROOT=~/scmroot}
export USRLOCAL SRCROOT BLDROOT ZIPROOT SCMROOT

export LC_COLLATE=C
#unset LANG
#unset LC_TIME

path=($ZDOTDIR/bin $USRLOCAL/bin /usr/X11R6/bin /usr/bin /bin /etc)

# Invoke system specific customizations
system_specific .zshenv -post

# Clean up path: remove trailing slashes and non-existant directories
# and resolve symbolic links
path=($^path(N))
path=(${^path%%/##})
foreach p in $path; do
  [[ -L $p ]] && path[(r)$p]=$(cd $p; pwd -r)
done

# Local Variables:
# mode: ksh
# sh-indentation: 2
# indent-tabs-mode: nil
# End:
